// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath("com.android.tools.build:gradle:3.5.3")
    classpath("com.github.ben-manes:gradle-versions-plugin:0.25.0")
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

tasks.wrapper {
  gradleVersion = "6.1"
  distributionType = Wrapper.DistributionType.ALL
}

apply plugin: "com.github.ben-manes.versions"

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+$/
  return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
  // Disallow release candidates as upgradable versions from stable versions
  resolutionStrategy {
    componentSelection {
      all {
        if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
          reject('Release candidate')
        }
      }
    }
  }

  // Optional parameters
  checkForGradleUpdate = true
  outputFormatter = "json"
  outputDir = "build/dependencyUpdates"
  reportfileName = "report"
}
